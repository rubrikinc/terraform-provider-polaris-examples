terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = ">=6.0.0"
    }
    polaris = {
      source  = "rubrikinc/polaris"
      version = ">=1.2.0"
    }
    time = {
      source  = "hashicorp/time"
      version = ">=0.13.1"
    }
  }
}

variable "account_id" {
  description = "AWS account ID."
  type        = string
}

variable "account_name" {
  description = "AWS account name."
  type        = string
}

variable "rsc_user_arn" {
  type        = string
  description = "RSC user ARN."
}

# Data known up front which is needed by both teams.
locals {
  external_id = "my-external-id"

  features = {
    CLOUD_NATIVE_PROTECTION = {
      permission_groups = [
        "BASIC"
      ]
    },
    EXOCOMPUTE = {
      permission_groups = [
        "BASIC",
        "RSC_MANAGED_CLUSTER"
      ]
    },
  }
}

module "team1" {
  source = "./team1"

  # Data known up front which is needed by both teams.
  external_id = local.external_id
  features    = local.features

  # Data known up front which is needed by team 1.
  rsc_user_arn = var.rsc_user_arn
}

module "team2" {
  source = "./team2"

  # Data known up front which is needed by both teams.
  external_id = local.external_id
  features    = local.features

  # Data known up front which is needed by team 2.
  account_id   = var.account_id
  account_name = var.account_name
  regions      = ["us-east-2"]

  # Data generated by team 1 that needs to be shared with team 2.
  instance_profiles = module.team1.instance_profiles
  roles             = module.team1.roles
}
